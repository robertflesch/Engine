/*==============================================================================
Copyright 2011-2015 Robert Flesch
All rights reserved.  This product contains computer programs, screen
displays and printed documentation which are original works of
authorship protected under United States Copyright Act.
Unauthorized reproduction, translation, or display is prohibited.
==============================================================================*/
package com.voxelengine.worldmodel
{
import com.voxelengine.events.WindowSplashEvent;
import com.voxelengine.Log;
import com.voxelengine.Globals;
import com.voxelengine.events.LoadingEvent;
import com.voxelengine.events.ModelEvent;
import com.voxelengine.events.RegionEvent;
import com.voxelengine.events.RoomEvent;
import com.voxelengine.events.PersistanceEvent;
import com.voxelengine.server.Network;
import com.voxelengine.server.Room;
import com.voxelengine.worldmodel.inventory.InventoryManager;
import com.voxelengine.worldmodel.models.ModelLoader;
import com.voxelengine.worldmodel.models.ModelDataManager;
import com.voxelengine.worldmodel.models.MetadataManager;
import com.voxelengine.worldmodel.models.ModelInfoManager;

/**
 * ...
 * @author Bob
 */
public class RegionManager 
{
	private var _regions:Vector.<Region> = null
	private var _currentRegion:Region = null
	private var _modelLoader:ModelLoader = new ModelLoader();
	private var _initialized:Boolean;
	
	public function get size():int { return _regions.length; }
	
	public function get currentRegion():Region { return _currentRegion; }
	public function set currentRegion(val:Region):void { 
		_currentRegion = val; 
		Log.out("RegionManager.currentRegion - set to: " + val.guid, Log.DEBUG ) 
	}
	
	public function get regions():Vector.<Region> { return _regions; }
	
	public function get modelLoader():ModelLoader { return _modelLoader; }
	
	public function RegionManager():void 
	{
		_regions = new Vector.<Region>;

		RegionEvent.addListener( RegionEvent.LOAD, 			regionLoad ); 
		RegionEvent.addListener( RegionEvent.JOIN, 			requestServerJoin ); 
		RegionEvent.addListener( RegionEvent.CHANGED, 		regionChanged );	
		RegionEvent.addListener( RegionEvent.TYPE_REQUEST, 	regionTypeRequest );
		RegionEvent.addListener( RegionEvent.REQUEST, 		regionRequest );	
		
		RoomEvent.addListener( RoomEvent.ROOM_DISCONNECT, 	requestDefaultRegionLoad );
		RoomEvent.addListener( RoomEvent.ROOM_JOIN_SUCCESS, onJoinRoomEvent );
		
		PersistanceEvent.addListener( PersistanceEvent.LOAD_SUCCEED, 	loadSucceed );			
		PersistanceEvent.addListener( PersistanceEvent.LOAD_FAILED, 	loadFail );			
		PersistanceEvent.addListener( PersistanceEvent.CREATE_SUCCEED, 	regionCreatedHandler ); 
		
		Globals.g_app.addEventListener( ModelEvent.PARENT_MODEL_ADDED,	regionModelChanged );
		Globals.g_app.addEventListener( ModelEvent.PARENT_MODEL_REMOVED,regionModelChanged );
									  
		LoadingEvent.addListener( LoadingEvent.MODEL_LOAD_FAILURE,removeFailedObjectFromRegion );									  
		LoadingEvent.addListener( LoadingEvent.LOAD_CONFIG_COMPLETE, configComplete );
		
		
		
		// This adds the event handlers
		// Is there a central place to do this?
		MetadataManager.init();
		ModelInfoManager.init();
		ModelDataManager.init();
		// This causes the to load its caches and listeners
		InventoryManager.init();
		MouseKeyboardHandler.init();
	}
	
	/**
	 * @param  - RegionEvent generated by a region when it has 
	 * @return - None
	 * Generates Event RegionEvent.UNLOAD if it is the current region
	*/
	private function regionLoad( $re:RegionEvent ):void
	{
		Log.out( "RegionManager.load - region: " + $re.guid, Log.DEBUG );
		WindowSplashEvent.dispatch( new WindowSplashEvent( WindowSplashEvent.CREATE ) );
		
		if ( currentRegion )
			RegionEvent.dispatch( new RegionEvent( RegionEvent.UNLOAD, currentRegion.guid ) );

		var region:Region = regionGet( $re.guid );
		if ( region ) {
			currentRegion = region;
			region.load();
		}
		else
			throw new Error( "RegionManager.regionLoad - trying to load I region I don't have in cache", Log.WARN );
	}
	
	private function regionRequest( $re:RegionEvent):void 	{
		
		if ( null == $re.guid ) {
			Log.out( "RegionManager.regionRequest guid rquested is NULL: ", Log.WARN );
			return;
		}
		Log.out( "RegionManager.regionRequest guid: " + $re.guid, Log.INFO );
		var region:Region = regionGet( $re.guid );
		if ( region ) {
			RegionEvent.dispatch( new RegionEvent( RegionEvent.ADDED, region.guid, region ) );
			return;
		}
		
		if ( true == Globals.online )
			PersistanceEvent.dispatch( new PersistanceEvent( PersistanceEvent.LOAD_REQUEST, Globals.DB_TABLE_REGIONS, $re.guid ) );
		else	
			PersistanceEvent.dispatch( new PersistanceEvent( PersistanceEvent.LOAD_REQUEST, Globals.REGION_EXT, $re.guid, null, null ) );			
	}
	
	private function regionTypeRequest(e:RegionEvent):void {
		if ( false == _initialized ) {
			PersistanceEvent.dispatch( new PersistanceEvent( PersistanceEvent.LOAD_REQUEST_TYPE, Globals.DB_TABLE_REGIONS, Network.PUBLIC, null, Globals.DB_TABLE_INDEX_OWNER ) );			
			PersistanceEvent.dispatch( new PersistanceEvent( PersistanceEvent.LOAD_REQUEST_TYPE, Globals.DB_TABLE_REGIONS, Network.userId, null, Globals.DB_TABLE_INDEX_OWNER ) );			
		}
			
		_initialized = true;
		// Get a list of what we currently have
		for each ( var region:Region in _regions ) {
			if ( region && region.owner == e.guid )
				RegionEvent.dispatch( new RegionEvent( RegionEvent.ADDED, region.guid, region ) );
		}
	}
	
	private function loadFail( $pe:PersistanceEvent ):void 
	{
		if ( Globals.DB_TABLE_REGIONS != $pe.table && Globals.REGION_EXT != $pe.table )
			return;
			
		Log.out( "RegionManager.loadFail - region: " + $pe.guid, Log.ERROR );
		throw new Error( "RegionManager.loadFail - why did I fail to load region PersistanceEvent: " + $pe.toString(), Log.WARN );
	}
	
	private function loadSucceed( $pe:PersistanceEvent ):void 
	{
		if ( Globals.DB_TABLE_REGIONS == $pe.table ) {
			Log.out( "RegionManager.loadSucceed - creating new region: " + $pe.guid, Log.DEBUG );
			var newRegion:Region = new Region( $pe.guid );
			newRegion.loadFromDBO( $pe.dbo );
			regionAdd( newRegion );
		}
		else if ( Globals.REGION_EXT == $pe.table ) {
			var startingRegion:Region = new Region( $pe.guid );
			startingRegion.initJSON( $pe.data );
			regionAdd( startingRegion );
			
			RegionEvent.dispatch( new RegionEvent( RegionEvent.LOAD, $pe.guid, startingRegion ) );
		}
	}
	
	private function regionAdd( $region:Region ):void {
		Log.out( "RegionManager.regionAdd - adding region: " + $region.guid, Log.DEBUG );
		if ( false == regionHas( $region.guid ) ) {
			_regions.push( $region );
			RegionEvent.dispatch( new RegionEvent( RegionEvent.ADDED, $region.guid, $region ) );
		}
		else
			Log.out( "RegionManager.regionAdd - NOT loading duplicate region: " + $region.guid, Log.DEBUG );
	}
	
	private function regionChanged( $re:RegionEvent):void 
	{
		var region:Region = regionGet( $re.guid );
		if ( region )
			region.save();
		else	
			Log.out( "RegionManager.regionChanged- did not find: " + $re.guid + " in order to save", Log.ERROR );
	}
	
	
	private function regionModelChanged( me:ModelEvent ):void { 
		var region:Region = currentRegion;
		if ( region && region.loaded )
			currentRegion.changed = true;
	}
	
	private function removeFailedObjectFromRegion( $e:LoadingEvent):void {
		// Do I need to remove this failed load?
		Log.out( "RegionManager.removeFailedObjectFromRegion - failed to load: " + $e.guid, Log.ERROR );
		currentRegion.changedForce = true;
		// Dont want to save if partially loaded
		//currentRegion.save();
	}
	
	////////////////////////////////////////////////////////////////////////////////////////
	////////////////////////////////////////////////////////////////////////////////////////
	
	public function configComplete( $e:LoadingEvent ):void
	{
		//startWithEmptyRegion( $e.guid );
		// This is used for testing
		loadRegionFromLocal( $e.guid )
	}
	public function startWithEmptyRegion( $guid:String ):void
	{
		var startingRegion:Region = new Region( $guid );
		startingRegion.createEmptyRegion();
		regionAdd( startingRegion );
		RegionEvent.dispatch( new RegionEvent( RegionEvent.LOAD, startingRegion.guid ) ); 
		RegionEvent.dispatch( new RegionEvent( RegionEvent.LOAD_COMPLETE, startingRegion.guid ) );
		// This tells the config manager that the local region was loaded and is ready to load rest of data.
	}
	
	public function loadRegionFromLocal( $guid:String ):void
	{
		RegionEvent.dispatch( new RegionEvent( RegionEvent.REQUEST, $guid ) );
	}
	
	/////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////
	static public function requestServerJoin( e:RegionEvent ):void {
		Log.out( "RegionManager.requestServerJoin - guid: " + e.guid, Log.DEBUG );
		Room.createJoinRoom( e.guid );	
	}
	
	static public function requestDefaultRegionLoad( e:RoomEvent ):void {
		Log.out( "RegionManager.requestDefaultRegionLoad", Log.DEBUG );
		var defaultRegionJSON:Object = Globals.g_app.configManager.defaultRegionJson;
		var defaultRegionID:String = defaultRegionJSON.config.region.startingRegion;
		Room.createJoinRoom( defaultRegionID );	
	}
	
	public function onJoinRoomEvent( e:RoomEvent ):void {
		Log.out( "RegionManager.onJoinRoomEvent - guid: " + e.guid, Log.DEBUG );
		RegionEvent.dispatch( new RegionEvent( RegionEvent.LOAD, e.guid ) );
	}
	
	/////////////////////////////////////////////////////////////////////////////////////////
	/////////////////////////////////////////////////////////////////////////////////////////
	
	// this calls the region and its model manager to update
	public function update( $elapsed:int ):void {
		if ( currentRegion )
			currentRegion.update( $elapsed );
	}
	
	// Just assign the dbo from the create to the region
	private function regionCreatedHandler( $pe:PersistanceEvent ):void {
		if ( Globals.DB_TABLE_REGIONS != $pe.table )
			return;
		
		Log.out( "RegionManager.regionCreatedHandler: " + $pe.guid );
		// check for duplicates
		var region:Region = regionGet( $pe.guid )
		if ( region )
			region.databaseObject = $pe.dbo;
		else	
			Log.out( "RegionManager.regionCreatedHandler: ERROR region not found for returned guid: " + $pe.guid );
	}
	
	/**
	 * @param  - guid of region
	 * @return - region or null
	 * 
	*/
	private function regionGet( $guid:String ):Region
	{
		for each ( var region:Region in _regions ) {
			if ( region && region.guid == $guid ) {
				return region;
			}
		}
		return null;
	}
	
	public function regionHas( $guid:String ):Boolean
	{
		for each ( var region:Region in _regions ) {
			if ( region && region.guid == $guid ) {
				return true;
			}
		}
		return false;
	}

	public function save():void
	{
		Log.out( "RegionManager.save", Log.DEBUG );
		if ( true == Globals.online && true == Globals.inRoom ) {
			currentRegion.save()
		}
	}
	
} // RegionManager
} // Package